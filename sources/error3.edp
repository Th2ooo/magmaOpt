/* Calculation of the compliance of the input structure */
include "./sources/inout.idp"
include "./sources/macros.idp"
load "msh3"
load "iovtk"

/* Get mesh and sol names, and global parameters */
string MESH       = getsParam(EXCHFILE,"MeshName");
string MESHT      = getsParam(EXCHFILE,"ObjMesh");
string SOL        = getsParam(EXCHFILE,"DispName");
string SOLT       = getsParam(EXCHFILE,"ObjDisp");
int REFEXT        = getiParam(EXCHFILE,"Refext");
int REFISO        = getiParam(EXCHFILE,"ReferenceBnd");
int REFUP       = getiParam(EXCHFILE,"Refup");


real hea, inc, w ;
hea = 6.0580;
inc = 0.58119 ;
w = 10 ;
macro UtoLOS(ux,uy,uz) -cos(hea)*sin(inc)*ux + sin(hea)*sin(inc)*uy + cos(inc)*uz // End of macro
real[int] l = [-cos(hea)*sin(inc), sin(hea)*sin(inc),cos(inc) ] ;



/* Loading mesh and target mesh*/
mesh3 Th  = readmesh3(MESH);
mesh3 ThT = readmesh3(MESHT);

/* Finite element spaces and functions */
fespace Vh(Th,P1);
fespace VhT(ThT,P1);
Vh ux,uy,uz,losdate,errmap,utemx,utemy,utemz;
VhT uxT,uyT,uzT,losdat;

/* Other parameters */
real err;

/* Read solution and observation */
loadvec3(SOL,ux[],uy[],uz[]); // displacement of the current shape
loadvec3(SOLT,uxT[],uyT[],uzT[]); // objective displacement

//Projection in LOS geometry
losdat = UtoLOS(uxT,uyT,uzT) ;


/* Interpolation from ThT to Th*/
losdate = losdat;
utemx = uxT;
utemy = uyT;
utemz = uzT;

/* Calculate objective */
err = int2d(Th,REFUP)(w*(l[0]*ux+l[1]*uy+l[2]*uz-l[0]*utemx-l[1]*utemy-l[2]*utemz)^2 );

/* Save result */
setrParam(EXCHFILE,"Error",err);

errmap = w*(UtoLOS(ux,uy,uz)-losdate)^2;

// VTU output for debuging
savevtk("obju.vtu", ThT, [uxT,uyT,uzT]);
savevtk("objulos.vtu", Th, losdate);

savevtk("curru.vtu", Th, [ux,uy,uz]);
savevtk("currulos.vtu", Th, UtoLOS(ux,uy,uz));
savevtk("errmap.vtu", Th, errmap);


