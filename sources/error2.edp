/* Calculation of the ERROR of the input structure */
include "./sources/inout.idp"
include "./sources/macros.idp"

load "msh3"
load "iovtk"


//// Functions
func real UtoLOSi(real ux, real uy, real uz, int i){
    /*Conversion to LOS geometry of track i by loading its geometry from exchfile*/
    real inc        = getrParam(EXCHFILE,"Incl"+i);
    real hea        = getrParam(EXCHFILE,"Head"+i);
    return -cos(hea)*sin(inc)*ux + sin(hea)*sin(inc)*uy + cos(inc)*uz ;
}


//// Parameters
/* Get mesh and sol names, and global parameters */
string MESH       = getsParam(EXCHFILE,"MeshName");
string SOL        = getsParam(EXCHFILE,"DispName");
int REFUP        = getiParam(EXCHFILE,"Refup");
string MESHI      = getsParam(EXCHFILE,"ObjMesh"); //mesh on which insar data is interpolated
int ntck        = getiParam(EXCHFILE,"Ntracks");
real err, w;
string TMPLOS;
/* Loading current mesh and insar data mesh*/
mesh3 Th = readmesh3(MESH);
mesh3 ThI = readmesh3(MESHI);



/* Finite element spaces and functions */
fespace Vh(Th,P1); //Current mesh fespace
Vh ux,uy,uz,tmplosp,ulosi;
Vh[int] losdatA(ntck); // fespace array to contain the tracks interpolated on current mesh

fespace VhI(ThI,P1); //Mesh of insar data fespace
VhI tmplos ; //tmplos los disp to load data


/* Read current displaement solution */
loadvec3(SOL,ux[],uy[],uz[]);


/* Loop over the different data tracks for interpolation */
for (int i = 0; i < ntck; ++i){
    cout << "Interpolating Track"+i << endl;
    TMPLOS       = getsParam(EXCHFILE,"Track"+i);
    loadsol(TMPLOS,tmplos[]); //read insar data
    tmplosp = tmplos ; // project it on the current mesh
    losdatA[i] = tmplosp; //save it in the array
}


/*Compute error function for N tracks*/
err = 0.0 ;
for (int i = 0; i < ntck; ++i){
    cout << "Error Track"+i << endl;
    w   = getrParam(EXCHFILE,"Weight"+i);
    err = err + w*int2d(Th,REFUP)((UtoLOSi(ux,uy,uz,i)-losdatA[i])^2);
}


/* Save result */
setrParam(EXCHFILE,"Error",err);
cout << "ERROR : " << err << endl ;


/*VTK output of the LOS for vizu*/
// for (int i = 0; i < ntck; ++i){
//     savevtk("objlos"+i+".vtu", Th,losdatA[i]); // data
//     ulosi = UtoLOSi(ux,uy,uz,i);
//     savevtk("ulos"+i+".vtu",Th,ulosi);  // current LOS disp
// }
