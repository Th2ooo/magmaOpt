/* Calculation of the ERROR of the input structure */
include "./sources/inout.idp"
include "./sources/macros.idp"
load "msh3"
load "iovtk"


//// Parameters
/* Get mesh and sol names, and global parameters */
string MESH       = getsParam(EXCHFILE,"MeshName");
string SOL        = getsParam(EXCHFILE,"DispName");
int REFUP        = getiParam(EXCHFILE,"Refup");
string MESHI      = getsParam(EXCHFILE,"ObjMesh"); //mesh on which insar data is interpolated
int ntck        = getiParam(EXCHFILE,"Ntracks");
real err, w;
string TMPLOS;
/* Loading current mesh and insar data mesh*/
mesh3 Th = readmesh3(MESH);
mesh3 ThI = readmesh3(MESHI);

//arrays of InSar Parameters
real[int] heas(ntck), incs(ntck), ws(ntck) ;
for (int i = 0; i < ntck; ++i){
    incs[i] = getrParam(EXCHFILE,"Incl"+i);
    heas[i] = getrParam(EXCHFILE,"Head"+i);
    ws[i] = getrParam(EXCHFILE,"Weight"+i);
}
// LOS geometry conversion
macro UtoLOSi(ux,uy,uz,i) -cos(heas[i])*sin(incs[i])*ux + sin(heas[i])*sin(incs[i])*uy + cos(incs[i])*uz // End of macro



/* Finite element spaces and functions */
fespace Vh(Th,P1); //Current mesh fespace
Vh ux,uy,uz,tmplosp,ulosi;
Vh[int] losdatA(ntck); // fespace array to contain the tracks interpolated on current mesh

fespace VhI(ThI,P1); //Mesh of insar data fespace
VhI tmplos ; //tmplos los disp to load data


/* Read current displaement */
loadvec3(SOL,ux[],uy[],uz[]);


/* Loop over the different data tracks for interpolation */
for (int i = 0; i < ntck; ++i){
    cout << "Interpolating Track"+i << endl;
    TMPLOS       = getsParam(EXCHFILE,"Track"+i);
    loadsol(TMPLOS,tmplos[]); //read insar data
    tmplosp = tmplos ; // project it on the current mesh
    losdatA[i] = tmplosp; //save it in the array
}


/*Compute error function for N tracks*/
err = 0.0 ;
for (int i = 0; i < ntck; ++i){
    cout << "Error Track"+i << endl;
    w   = getrParam(EXCHFILE,"Weight"+i);
    err = err + w*int2d(Th,REFUP)((UtoLOSi(ux,uy,uz,i)-losdatA[i])^2);
}


/* Save result */
setrParam(EXCHFILE,"Error",err);
cout << "ERROR : " << err << endl ;

real a = UtoLOSi(1,1,1,0) ;
cout << "aa : " << a << endl ;


/*VTK output of the LOS for vizu*/
for (int i = 0; i < ntck; ++i){
    savevtk("objlos"+i+".vtu", Th,losdatA[i]); // data
    ulosi = UtoLOSi(ux,uy,uz,i);
    savevtk("ulos"+i+".vtu",Th,ulosi);  // current LOS disp
}
