/* Resolution of the linearized elasticity equation on the input shape */
include "./sources/inout.idp"
include "./sources/macros.idp"
load "medit"
load "msh3"

/* Get mesh and sol names, and global parameters */
string MESH       = getsParam(EXCHFILE,"MeshName");
string SOL        = getsParam(EXCHFILE,"DispName");
string ADJ        = getsParam(EXCHFILE,"AdjName");
string MESHT      = getsParam(EXCHFILE,"ObjMesh");
string SOLT       = getsParam(EXCHFILE,"ObjDisp");
int REFEXT        = getiParam(EXCHFILE,"Refext");
int REFDIR        = getiParam(EXCHFILE,"Dirichlet");
int REFISO        = getiParam(EXCHFILE,"ReferenceBnd");
int REFUP       = getiParam(EXCHFILE,"Refup");


real hea, inc, w ;
hea = 6.0580;
inc = 0.58119 ;
w = 10;
macro UtoLOS(ux,uy,uz) -cos(hea)*sin(inc)*ux + sin(hea)*sin(inc)*uy + cos(inc)*uz // End of macro
real[int] l = [-cos(hea)*sin(inc), sin(hea)*sin(inc),cos(inc) ] ;



/* Loading mesh */
mesh3 Th = readmesh3(MESH);
mesh3 ThT = readmesh3(MESHT);

/* Finite element spaces and functions */
fespace Vh(Th,P1);
fespace VhT(ThT,P1);
fespace Vh0(Th,P0);
Vh ux,uy,uz,px,py,pz;
VhT uxT,uyT,uzT,losdat;
Vh0 reg = region;

/* Read solution and observation */
loadvec3(SOL,ux[],uy[],uz[]);
loadvec3(SOLT,uxT[],uyT[],uzT[]);

/* Mesh of the interior part and corresponding FE space */
mesh3 The = trunc(Th,(reg(x,y,z) == REFEXT),label=REFEXT);
fespace Vhe(The,P1);
Vhe uex,uey,uez,pex,pey,pez,vex,vey,vez,losdate,utemx,utemy,utemz;

/* Interpolation of displacement from full to sub mesh */
uex = ux;
uey = uy;
uez = uz;

/* Interpolation from ThT to The */


//Projection in LOS geometry
losdat = UtoLOS(uxT,uyT,uzT) ;


/* Interpolation from ThT to Th*/
losdate = losdat;
utemx = uxT;
utemy = uyT;
utemz = uzT;


/* Variational formulation of the problem */
problem adjoint([pex,pey,pez],[vex,vey,vez],solver=CG) = int3d(The)(2.0*mu*(dx(pex)*dx(vex)+dy(pey)*dy(vey)+dz(pez)*dz(vez)))
                                   + int3d(The)(mu*((dx(pey)+dy(pex))*(dx(vey)+dy(vex)) + (dx(pez)+dz(pex))*(dx(vez)+dz(vex)) + (dy(pez)+dz(pey))*(dy(vez)+dz(vey))))
                                   + int3d(The)(lm*(dx(pex)+dy(pey)+dz(pez))*(dx(vex)+dy(vey)+dz(vez)))
                                   + int2d(The,REFUP)(2.0*(l[0]*ux+l[1]*uy+l[2]*uz-l[0]*utemx-l[1]*utemy-l[2]*utemz)*(l[0]*vex+l[1]*vey+l[2]*vez))
                                   + on(REFDIR,pex=0.0,pey=0.0,pez=0.0);

/* Solve problem */
adjoint;

/* Transfer the problem on the full mesh */
px = pex;
py = pey;
pz = pez;

/* Save solution */
printvec3(ADJ,px[],py[],pz[]);
