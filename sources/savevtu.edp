/* Resolution of the linearized elasticity equation on the input shape */
include "./sources/inout.idp"
include "./sources/macros.idp"
load "medit"
load "msh3"
load "iovtk"

/* Get mesh and sol names, and global parameters */
string MESH       = getsParam(EXCHFILE,"MeshName");
string SOL        = getsParam(EXCHFILE,"DispName");
int REFINT          = getiParam(EXCHFILE,"Refint");
int REFEXT          = getiParam(EXCHFILE,"Refext");
int REFDIR        = getiParam(EXCHFILE,"Dirichlet");
int REFNEU        = getiParam(EXCHFILE,"Neumann");
int REFISO        = getiParam(EXCHFILE,"ReferenceBnd");

/* Loading mesh */
mesh3 Th = readmesh3(MESH);
cout << "meshname" << MESH<< endl;
cout << "labels" << labels(Th)<< endl;
cout << "regions" << regions(Th)<< endl;


/* Finite element spaces and functions */
fespace Vh(Th,P1);
fespace Vh0(Th,P0);
Vh ux,uy,uz;
Vh0 reg = region;

// savemesh(Th,"elasticitt.mesh");

/* Mesh of the interior part and corresponding FE space */
mesh3 Thi = trunc(Th,(reg(x,y,z) == REFINT),label=REFINT);

// medit("full",Th);
// mesh3 ell = trunc(Th,region=3) ;
// medit("ell",ell);
// medit("Interior",Thi);


fespace Vhi(Thi,P1);
Vhi uix,uiy,uiz,vix,viy,viz;

/* Variational formulation of the problem :
 MODIFICATION TO HAVE :
    - T = DP.n on source*/
problem elas([uix,uiy,uiz],[vix,viy,viz],solver=CG) = int3d(Thi)(2.0*mu*(dx(uix)*dx(vix)+dy(uiy)*dy(viy)+dz(uiz)*dz(viz)))
                                   + int3d(Thi)(mu*((dx(uiy)+dy(uix))*(dx(viy)+dy(vix)) + (dx(uiz)+dz(uix))*(dx(viz)+dz(vix)) + (dy(uiz)+dz(uiy))*(dy(viz)+dz(viy))))
                                   + int3d(Thi)(lm*(dx(uix)+dy(uiy)+dz(uiz))*(dx(vix)+dy(viy)+dz(viz)))
                                   - int2d(Thi,REFNEU)(-DPs*(vix*(N.x)+viy*(N.y)+viz*(N.z))) //Negative pressure bc the normal is coming inside the source
                                   + on(REFDIR,uix=0.0,uiy=0.0,uiz=0.0);

/*
THEO : if i want to change the load case (uniform pressure over normal,
                                   then i'll have to compute the normal,
                                   wich is the gradient of the surface ;-((
                                       */

/* Solve problem */
elas;

/* Transfer the problem on the full mesh */
ux = uix;
uy = uiy;
uz = uiz;

// cout <<  "UXXXX" << ux << endl;
/* Save solution */
printvec3(SOL,ux[],uy[],uz[]);

int[int] Order = [1];
string[int] DataName = ["ux","uy","uz"];

savevtk("u.vtu", Th, [ux,uy,uz]);
