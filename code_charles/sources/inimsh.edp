/* Creation of the initial mesh: magma chamber reconstruction test case */
include "./sources/inout.idp"
include "./sources/macros.idp"
load "tetgen"
load "msh3"
load "iovtk"

/* Get mesh and sol names */
string MESH    = getsParam(EXCHFILE,"MeshName");
int    REFDIR  = getiParam(EXCHFILE,"Dirichlet");
int    REFTARG = getiParam(EXCHFILE,"Observation");

/* Create the meshes of the faces */
int N = 20;
border a(t=0,1.0) {x=0; y=1.0-t; label=0;};
border b(t=0,1.0) {x=t; y=0; label=0;};
border c(t=0,1.0) {x=1.0; y=t; label=0;};
border d(t=0,1.0) {x=1.0-t; y=1.0; label=0;};

/* Create Dirichlet boundary (bottom) */
mesh Th2d    = buildmesh(a(N) + b(N) + c(N) + d(N));
int[int] rch = [0,REFDIR];
Th2d         = change(Th2d,region=rch);
meshS ThD    = movemesh23(Th2d,transfo=[y,x,0.0]);

/* Create observation boundary (top) */
Th2d      = buildmesh(a(N)+b(N)+c(N)+d(N));
rch       = [0,REFTARG];
Th2d      = change(Th2d,region=rch);
meshS ThO = movemesh23(Th2d,transfo=[x,y,1.0]);

/* Create four lateral boundaries */
Th2d = buildmesh(a(N)+b(N)+c(N)+d(N));
meshS Thf = movemesh23(Th2d,transfo=[1.0,x,y]);
meshS Thb = movemesh23(Th2d,transfo=[0.0,x,1.0-y]);
meshS Thl = movemesh23(Th2d,transfo=[1.0-y,0.0,x]);
meshS Thr = movemesh23(Th2d,transfo=[1.0-y,1.0,1.0-x]);

/* Total surface mesh */
meshS ThS = ThD + ThO + Thf + Thb + Thl + Thr;

/* Create 3d mesh */
real[int] dominfo = [0.1,0.1,0.1,0,0.0001];
mesh3 Th3 = tetg(ThS,switch="pqaAAYYQ",nbofregions=1,regionlist=dominfo);

/* Save mesh */
savemesh(Th3,MESH);
savevtk("inimsh.vtu", Th3);
