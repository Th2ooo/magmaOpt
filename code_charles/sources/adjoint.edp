/* Resolution of the linearized elasticity equation on the input shape */
include "./sources/inout.idp"
include "./sources/macros.idp"
load "medit"
load "msh3"

/* Get mesh and sol names, and global parameters */
string MESH       = getsParam(EXCHFILE,"MeshName");
string SOL        = getsParam(EXCHFILE,"DispName");
string ADJ        = getsParam(EXCHFILE,"AdjName");
string MESHT      = getsParam(EXCHFILE,"MeshTarget");
string SOLT       = getsParam(EXCHFILE,"SolTarget");
int REFEXT        = getiParam(EXCHFILE,"Refext");
int REFDIR        = getiParam(EXCHFILE,"Dirichlet");
int REFISO        = getiParam(EXCHFILE,"ReferenceBnd");
int REFTARG       = getiParam(EXCHFILE,"Observation");

/* Loading mesh */
mesh3 Th = readmesh3(MESH);
mesh3 ThT = readmesh3(MESHT);

/* Finite element spaces and functions */
fespace Vh(Th,P1);
fespace VhT(ThT,P1);
fespace Vh0(Th,P0);
Vh ux,uy,uz,px,py,pz;
VhT uxT,uyT,uzT;
Vh0 reg = region;

/* Read solution and observation */
loadvec3(SOL,ux[],uy[],uz[]);
loadvec3(SOLT,uxT[],uyT[],uzT[]);

/* Mesh of the interior part and corresponding FE space */
mesh3 The = trunc(Th,(reg(x,y,z) == REFEXT),label=REFEXT);
fespace Vhe(The,P1);
Vhe uex,uey,uez,pex,pey,pez,vex,vey,vez,uetemx,uetemy,uetemz;

/* Interpolation of displacement from full to sub mesh */
uex = ux;
uey = uy;
uez = uz;

/* Interpolation from ThT to The */
uetemx = uxT;
uetemy = uyT;
uetemz = uzT;

/* Variational formulation of the problem */
problem adjoint([pex,pey,pez],[vex,vey,vez],solver=CG) = int3d(The)(2.0*mu*(dx(pex)*dx(vex)+dy(pey)*dy(vey)+dz(pez)*dz(vez)))
                                   + int3d(The)(mu*((dx(pey)+dy(pex))*(dx(vey)+dy(vex)) + (dx(pez)+dz(pex))*(dx(vez)+dz(vex)) + (dy(pez)+dz(pey))*(dy(vez)+dz(vey))))
                                   + int3d(The)(lm*(dx(pex)+dy(pey)+dz(pez))*(dx(vex)+dy(vey)+dz(vez)))
                                   + int2d(The,REFTARG)(2.0*((uex-uetemx)*vex + (uey-uetemy)*vey + (uez-uetemz)*vez))
                                   + on(REFDIR,pex=0.0,pey=0.0,pez=0.0);

/* Solve problem */
adjoint;

/* Transfer the problem on the full mesh */
px = pex;
py = pey;
pz = pez;

/* Save solution */
printvec3(ADJ,px[],py[],pz[]);
