/* Resolution of the linearized elasticity equation on the input shape */
include "./sources/inout.idp"
include "./sources/macros.idp"
load "medit"
load "msh3"
load "iovtk"

/* Get mesh and sol names, and global parameters */
string MESH       = getsParam(EXCHFILE,"MeshName");
string SOL        = getsParam(EXCHFILE,"DispName");
int REFEXT        = getiParam(EXCHFILE,"Refext");
int REFDIR        = getiParam(EXCHFILE,"Dirichlet");
int REFISO        = getiParam(EXCHFILE,"ReferenceBnd");

/* Loading mesh */
mesh3 Th = readmesh3(MESH);

/* Finite element spaces and functions */
fespace Vh(Th,P1);
fespace Vh0(Th,P0);
Vh ux,uy,uz;
Vh0 reg = region;

/* Mesh of the interior part and corresponding FE space */
mesh3 The = trunc(Th,(reg(x,y,z) == REFEXT),label=REFEXT);

fespace Vhe(The,P1);
Vhe uex,uey,uez,vex,vey,vez;

/* Variational formulation of the problem */
problem elas([uex,uey,uez],[vex,vey,vez],solver=CG) = int3d(The)(2.0*mu*(dx(uex)*dx(vex)+dy(uey)*dy(vey)+dz(uez)*dz(vez)))
                                   + int3d(The)(mu*((dx(uey)+dy(uex))*(dx(vey)+dy(vex)) + (dx(uez)+dz(uex))*(dx(vez)+dz(vex)) + (dy(uez)+dz(uey))*(dy(vez)+dz(vey))))
                                   + int3d(The)(lm*(dx(uex)+dy(uey)+dz(uez))*(dx(vex)+dy(vey)+dz(vez)))
                                   + int2d(The,REFISO)(Deltap*(N.x*vex+N.y*vey+N.z*vez))
                                   + on(REFDIR,uex=0.0,uey=0.0,uez=0.0);

/* Solve problem */
elas;

/* Transfer the problem on the full mesh */
ux = uex;
uy = uey;
uz = uez;

/* Save solution */
printvec3(SOL,ux[],uy[],uz[]);

cout << MESH ;
savevtk("u.vtu", Th, [ux,uy,uz]);
